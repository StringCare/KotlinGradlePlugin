plugins {
    id 'java'
    id 'com.jfrog.bintray' version '1.7.1'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "com.github.dcendents.android-maven" version "2.1"
}

group 'com.stringcare'
version '3.0'

def siteUrl = 'https://github.com/StringCare/GradlePlugin'
def gitUrl = 'https://github.com/StringCare/GradlePlugin.git'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation group: 'com.google.guava', name: 'guava', version: '28.0-jre'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'StringCareAndroidPlugin'
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'efraespada'
                        name 'efraespada'
                        email 'efraespada@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "StringCareAndroidPlugin"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

processResources {
    from 'src/main/kotlin/components/jni'
    include '*.dylib', '*.dll'
}
